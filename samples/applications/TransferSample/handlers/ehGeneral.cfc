<!--------------------------------------------------------------------
Generated Application: TransferSample
Generated Date: March 19 2007
Generated By: ColdBox Ant Task.
---------------------------------------------------------------------->
<!-----------------------------------------------------------------------
Template : ehGeneral.cfc
Author 	 : Luis Majano
Date     : 3/19/2007 3:28:35 PM
Description :
	This is the main handler for the Transfer sample

Modification History:
3/19/2007 - Created Template
----------------------------------------------------------------------><cfcomponent name="ehGeneral" extends="coldbox.system.eventhandler">	<cffunction name="onAppInit" access="public" returntype="void" output="false">		<cfargument name="Event" type="coldbox.system.beans.requestContext">		<!--- ON Application Start Here --->
		<!--- Create our Transfer Factory --->
		<cfset var DSFile = getSetting("TransferSettings.datasourceFile")>
		<cfset var TFile = getSetting("TransferSettings.transferFile")>
		<cfset var Definitions = "/" & getSetting("AppMapping") & "/" & getSetting("TransferSettings.Definitions")>
		<cfset var TransferFactory = CreateObject("component","transfer.TransferFactory").init(DSFile,TFile,Definitions)>		<!--- Place in Cache Indefinetely --->
		<cfset getColdBoxOCM().set("TransferFactory",TransferFactory, 0)>
	</cffunction>	<cffunction name="onRequestStart" access="public" returntype="void" output="false">		<cfargument name="Event" type="coldbox.system.beans.requestContext">		<!--- On Request Start Code Here --->
		<cfset var rc = event.getCollection()>
		<!--- Set a title for my App--->
		<cfset rc.title = "Transfer Sample Application">
		<!--- Place the Transfer Factory in the event object for every Request --->
		<cfset rc.Transfer = getColdboxOCM().get("TransferFactory").getTransfer()>	</cffunction>	<cffunction name="dspHome" access="public" returntype="void" output="false">		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<!--- Set the View To Display --->		<cfset Event.setView("vwHome")>	</cffunction>

	<cffunction name="dspUsers" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<!--- Do Your Logic Here to prepare a view --->
		<cfset rc.Users = rc.Transfer.list("users.users","lname",false)>
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwListing")>
	</cffunction>

	<cffunction name="doDelete" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var i = 1>
		<!--- Do Your Logic Here to prepare a view --->
		<cfset event.paramValue("idlist","")>
		<cfloop list="#rc.idlist#" index="i">
			<cfset rc.Transfer.delete(rc.Transfer.get("users.users",i))>
			<cfset getPlugin("messagebox").setMessage("info","User(s) removed successfully")>
		</cfloop>
		<cfset setNextEvent("ehGeneral.dspUsers")>
	</cffunction>

	<cffunction name="dspAddUser" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwAdd")>
	</cffunction>

	<cffunction name="doAdd" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var oUser = "">
		<!--- Get Transfer Object --->
		<cfset oUser = rc.Transfer.new("users.users")>
		<!--- Populate it with RC data --->
		<cfset getPlugin("beanFactory").populateBean(oUser)>
		<!--- Add new User --->
		<cfset rc.Transfer.save(oUser)>
		<cfset getPlugin("messagebox").setMessage("info", "User inserted")>
		<!--- RElocate to listing --->
		<cfset setNextEvent("ehGeneral.dspUsers")>
	</cffunction>

	<cffunction name="dspEditUser" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<!--- Get User id --->
		<cfset rc.oUser = rc.Transfer.get("users.users",rc.id)>
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwEdit")>
	</cffunction>

	<cffunction name="doUpdate" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var oUser = "">
		<!--- Get Transfer Object --->
		<cfset oUser = rc.Transfer.get("users.users",rc.id)>
		<!--- Populate it with RC data --->
		<cfset getPlugin("beanFactory").populateBean(oUser)>
		<!--- Update User --->
		<cfset rc.Transfer.save(oUser)>
		<cfset getPlugin("messagebox").setMessage("info", "User Updated")>
		<!--- RElocate to listing --->
		<cfset setNextEvent("ehGeneral.dspUsers")>
	</cffunction>
</cfcomponent>